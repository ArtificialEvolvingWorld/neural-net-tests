Import('env')

env = env.Clone()

# Because otherwise too many warnings
env['CCFLAGS'] = [flag for flag in env['CCFLAGS'] if
                  flag not in ['-Wall','-Wextra','-pedantic']]
env.Append(CCFLAGS=['-Wno-narrowing'])

env.Append(CPPPATH = ['include', 'source/Irrlicht/zlib', 'source/Irrlicht/jpeglib', 'source/Irrlicht/libpng'] )
env.Append(CPPDEFINES = {'IRRLICHT_EXPORTS':1} )
env.Append(CPPDEFINES = ['PNG_THREAD_UNSAFE_OK', 'PNG_NO_MMX_CODE', 'PNG_NO_MNG_FEATURES'] )

libIrrlicht = env.SharedLibrary('Irrlicht',
                                Glob('source/Irrlicht/*.cpp') + Glob('source/Irrlicht/lzma/*.c') +
                                Glob('source/Irrlicht/zlib/*.c') + Glob('source/Irrlicht/jpeglib/*.c') +
                                Glob('source/Irrlicht/libpng/*.c') + Glob('source/Irrlicht/aesGladman/*.cpp') +
                                Glob('source/Irrlicht/bzip2/*.c'))[0]

libIrrlicht.attributes.usage = {
    'CPPPATH':Dir('include').RDirs('.'),
    'LIBS':['Irrlicht','GL','Xxf86vm','Xext','X11','Xcursor'],
    'LIBPATH':[libIrrlicht.dir],
    'CPPDEFINES':{'IRRLICHT_EXPORTS':1},
}

Return('libIrrlicht')
